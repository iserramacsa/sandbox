# FILES
SET (SOURCES messageFB.c)
SET (HEADERS messageFB.h)
SET (LIB_TARGET ${APP_NAME}_lib)

OPTION (WITH_BIN "Compile and install main application" ON)
SET (BIN_SOURCES main.c)
SET (BIN_HEADERS)
SET (BIN_TARGET ${APP_NAME}_bin)

IF (WITH_BIN)
	OPTION (WITH_SHELL "Install shell API file" ON)
	SET (SHELL_IN messageFB.sh.in)
	SET (SHELL_FILE ${APP_NAME}.sh)
ELSE ()
	SET (WITH_SHELL OFF CACHE BOOL "" FORCE)
ENDIF ()

OPTION (WITH_TEST "Compile and install test application" OFF)
SET (TEST_SOURCES test.c)
SET (TEST_HEADERS)
SET (TEST_TARGET ${APP_NAME}_test)

# VERSION INFORMATION
MACSA_CREATE_VERSION (VERSION_FILE)
LIST (APPEND HEADERS ${VERSION_FILE})

# CONFIGURE
IF (WITH_SHELL)
	CONFIGURE_FILE (${CMAKE_CURRENT_SOURCE_DIR}/${SHELL_IN}
			${CMAKE_CURRENT_BINARY_DIR}/${SHELL_FILE}
			@ONLY)
ENDIF ()

# COMPILE
FIND_PATH (DIRECTFB_INCLUDE_DIR directfb.h PATH_SUFFIXES directfb)
INCLUDE_DIRECTORIES (${DIRECTFB_INCLUDE_DIR})

# LINK
FIND_LIBRARY (LIB_directfb directfb)

# BUILD
ADD_LIBRARY (${LIB_TARGET} SHARED ${SOURCES} ${HEADERS})
TARGET_LINK_LIBRARIES (${LIB_TARGET} ${LIB_directfb})
SET_TARGET_PROPERTIES (${LIB_TARGET} PROPERTIES OUTPUT_NAME ${APP_LIB})

IF (WITH_BIN)
	ADD_EXECUTABLE (${BIN_TARGET} ${BIN_SOURCES} ${BIN_HEADERS})
	IF (FONT)
		TARGET_COMPILE_DEFINITIONS (${BIN_TARGET} PRIVATE -DFONT="${FONT}")
	ENDIF ()
	IF (COLOR)
		TARGET_COMPILE_DEFINITIONS (${BIN_TARGET} PRIVATE -DCOLOR=${COLOR})
	ENDIF ()
	TARGET_LINK_LIBRARIES (${BIN_TARGET} ${LIB_TARGET})
	SET_TARGET_PROPERTIES (${BIN_TARGET} PROPERTIES OUTPUT_NAME ${APP_BIN})
ENDIF ()

IF (WITH_TEST)
	ADD_EXECUTABLE (${TEST_TARGET} ${TEST_SOURCES} ${TEST_HEADERS})
	IF (TEST_FONT)
		TARGET_COMPILE_DEFINITIONS (${TEST_TARGET} PRIVATE -DTEST_FONT="${TEST_FONT}")
	ENDIF ()
	IF (TEST_COLOR)
		TARGET_COMPILE_DEFINITIONS (${TEST_TARGET} PRIVATE -DTEST_COLOR=${TEST_COLOR})
	ENDIF ()
	TARGET_LINK_LIBRARIES (${TEST_TARGET} ${LIB_TARGET})
	SET_TARGET_PROPERTIES (${TEST_TARGET} PROPERTIES OUTPUT_NAME ${APP_NAME}_test)
ENDIF ()

# INSTALL
INSTALL (TARGETS ${LIB_TARGET} LIBRARY DESTINATION lib)
INSTALL (FILES ${HEADERS} DESTINATION include/messageFB)
IF (WITH_BIN)
	INSTALL (TARGETS ${BIN_TARGET} RUNTIME DESTINATION bin)
	IF (WITH_SHELL)
		INSTALL (PROGRAMS ${CMAKE_CURRENT_BINARY_DIR}/${SHELL_FILE} DESTINATION ../etc/default/)
	ENDIF ()
ENDIF ()
IF (WITH_TEST)
	INSTALL (TARGETS ${TEST_TARGET} RUNTIME DESTINATION bin)
ENDIF ()
